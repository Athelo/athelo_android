plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
    id "com.google.gms.google-services"
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.appdistribution'
}

android {
    compileSdk 32
    signingConfigs {
        def props = new Properties()
        file("../local.properties").withInputStream { props.load(it) }

        release {
            storeFile file('keystore')
            storePassword props.getProperty("debugStorePassword")
            keyAlias props.getProperty("debugAlias")
            keyPassword props.getProperty("debugKeyPassword")
        }

        debug {
            storeFile file('keystore')
            storePassword props.getProperty("debugStorePassword")
            keyAlias props.getProperty("debugAlias")
            keyPassword props.getProperty("debugKeyPassword")
        }
    }
    defaultConfig {
        applicationId "com.i2asolutions.athelo"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0.37"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        manifestPlaceholders = ['appAuthRedirectScheme': 'com.i2asolutions.athelo']
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            firebaseAppDistribution {
                serviceCredentialsFile = "./athelo-dev.json"
                releaseNotesFile = "./firebase-release-notes.txt"
                groups = "droid-test-group"
            }
        }
        rc {
            debuggable false
            signingConfig signingConfigs.debug
            firebaseAppDistribution {
                serviceCredentialsFile = "./athelo-dev.json"
                releaseNotesFile = "./firebase-release-notes.txt"
                groups = "droid-test-group"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        viewBinding true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    flavorDimensions 'server'
    productFlavors {
        dev {
            dimension 'server'
            applicationIdSuffix ".dev"
            buildConfigField "String", "BASE_URL", "\"https://i2a-social-api-qa.i2asolutions.com/\""
            buildConfigField "String", "BASE_WSS", "\"wss://chat-server-stg.i2asolutions.com/socket/\""
            buildConfigField "String", "APP_ID", "\"ATHELO-QA\""
            buildConfigField "String", "GOOGLE_CLIENT_ID", "\"85793544772-5l6d8nibk5j152shv4md9pi7ssjvjhev.apps.googleusercontent.com\""
            resValue "string", "app_name", "\"Athelo-Dev\""
            manifestPlaceholders = ['appAuthRedirectScheme': 'com.i2asolutions.athelo.dev']
        }
        qa {
            dimension 'server'
            applicationIdSuffix ".qa"
            buildConfigField "String", "BASE_URL", "\"https://i2a-social-api-qa.i2asolutions.com/\""
            buildConfigField "String", "BASE_WSS", "\"wss://chat-server-stg.i2asolutions.com/socket/\""
            buildConfigField "String", "APP_ID", "\"ATHELO-QA\""
            buildConfigField "String", "GOOGLE_CLIENT_ID", "\"85793544772-q18v4is5ov7jjbikchahdqvp9cm5iud6.apps.googleusercontent.com\""
            resValue "string", "app_name", "\"Athelo-Qa\""
            manifestPlaceholders = ['appAuthRedirectScheme': 'com.i2asolutions.athelo.qa']
        }
        prod {
            dimension 'server'
            buildConfigField "String", "BASE_URL", "\"https://i2a-social-api-qa.i2asolutions.com/\""
            //todo: Change when ready
            buildConfigField "String", "APP_ID", "\"ATHELO-PROD\""
            buildConfigField "String", "GOOGLE_CLIENT_ID", "\"85793544772-k6vubtpevir49p5ih85rt5743vn23079.apps.googleusercontent.com\""
// todo: Change when ready
            resValue "string", "app_name", "\"Athelo\""
            manifestPlaceholders = ['appAuthRedirectScheme': 'com.i2asolutions.athelo']
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.0'
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'

    // Compose
    implementation "com.google.accompanist:accompanist-pager:0.24.9-beta"
    implementation "com.google.accompanist:accompanist-insets-ui:0.24.9-beta"
    implementation "com.google.accompanist:accompanist-pager-indicators:0.24.9-beta"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation 'androidx.compose.material3:material3:1.0.0-alpha14'
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.activity:activity-compose:1.5.0'
    implementation "com.google.accompanist:accompanist-swiperefresh:0.24.13-rc"
    implementation "com.google.accompanist:accompanist-flowlayout:0.24.13-rc"

    // Services
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    // Support libs
    implementation "androidx.appcompat:appcompat:$compat_version"
//    // Auth
//    implementation "com.google.android.gms:play-services-auth:20.2.0"
    implementation "net.openid:appauth:0.11.1"

    //image picker
    implementation 'com.github.dhaval2404:imagepicker:2.1'

    // libphonenumber
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.9.9'
    //firebase - waiting for setup project in console
    implementation platform('com.google.firebase:firebase-bom:29.0.4')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    //Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    //Coil for Compose
    implementation("io.coil-kt:coil-compose:2.0.0-rc02")
    // Constraint layout compose
    implementation "androidx.constraintlayout:constraintlayout-compose:1.1.0-alpha03"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$coroutines_version"

    //HTML
    implementation 'com.github.ireward:compose-html:1.0.2'

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'
    //Chrome Tabs
    implementation "androidx.browser:browser:1.4.0"
    //Splash
    implementation 'androidx.core:core-splashscreen:1.0.0-rc01'
    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}

kapt {
    correctErrorTypes true
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions.freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
}

task buildAndSendToFirebase(type: GradleBuild) {
    tasks = ['clean', 'assembleQaRc', 'appDistributionUploadQaRc']
}